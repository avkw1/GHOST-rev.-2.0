;-------------------------------------------------------------------------------
; Умножение на 500
;-------------------------------------------------------------------------------
; вход: LOOPH = значение (0-127)
; выход: LOOPH:LOOPL = результат
mul_500_proc:
    clr LOOPL
    lsl LOOPH                       ; LOOPH:LOOPL = value x 512
    mov tmp1,LOOPH
    clr tmp2
    lsl tmp1
    rol tmp2                        ; tmp2:tmp1 = value x 4
    sub LOOPL,tmp1
    sbc LOOPH,tmp2                  ; LOOPH:LOOPL = value x 508
    lsl tmp1
    rol tmp2                        ; tmp2:tmp1 = value x 8
    sub LOOPL,tmp1
    sbc LOOPH,tmp2                  ; LOOPH:LOOPL = value x 500
    ret

;-------------------------------------------------------------------------------
; Сдвиг 24-битного значения на 1 бит влево
;-------------------------------------------------------------------------------
; tmp3:tmp2:tmp1 = значение
lsl_24_proc:
    lsl tmp1
    rol tmp2
    rol tmp3
    ret

;-------------------------------------------------------------------------------
; Сложение 24-битных значений
;-------------------------------------------------------------------------------
; tmp4:LOOPH:LOOPL = значение 1 и сумма
; tmp3:tmp2:tmp1 = значение 2
add_24_proc:
    add LOOPL,tmp1
    adc LOOPH,tmp2
    adc tmp4,tmp3
    ret

;-------------------------------------------------------------------------------
; Процедура сдвига влево 32-битного значения
;-------------------------------------------------------------------------------
; tmp4:tmp3:tmp2:tmp1 = значение
; LOOPL = количество сдвигов
lsl_32_proc:
    lsl tmp1
    rol tmp2
    rol tmp3
    rol tmp4
    dec LOOPL
    brne lsl_32_proc
    ret

;-------------------------------------------------------------------------------
; Процедура копирования памяти
;-------------------------------------------------------------------------------
; X -> буфер
; Y -> данные для копирования
; LOOPL = количество байтов
memcpy:
    ld tmp1,Y+
    st X+,tmp1
    dec LOOPL
    brne memcpy
    ret
